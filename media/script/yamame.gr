event yamameBehavior(yamame: Actor) {
    var time = 0;

    loop yield {
        if (yamame.hasGraphic("yesbow")) {
            if (time == 120) {
                yamame.spiderWebRange(90f, 10f);
                time = 0;
            }
            
            var acc = @Vec2f.zero;
            var player = @World.getPlayer?;
            var px, py, pz = player.getPosition;
            var ex, ey, ez = yamame.getPosition;
            
            var dir = @Vec2f(px - ex, py - ey).normalized;
            var ang = dir.angle.deg;
            
            yamame.setAngle(ang + 90f);
            acc += dir * 0.8f;

            //yamame.accelerate(acc.x, acc.y, 0f);
            time++;
        }
    }
}

func spiderWebRange(yamame: Entity, R: float, r: float) {
    var angle = 0f;
    var dr = R - r;
    var ratio = dr / r;
    var ex, ey, ez = yamame.getPosition;

    @World.playSound("enemy_shot");
    while (angle < 360f) {
        var t = angle.rad;

        var dx = dr * cos(t) + r * cos(ratio * t);
        var dy = dr * sin(t) - r * sin(ratio * t);
        var sx = ex + dx as <int>;
        var sy = ey + dy as <int>;

        var shot = @Shot("bullet");
        shot.setPosition(sx, sy, ez);
        shot.setAngle(angle);
        shot.fireAfterDelay(30, 1f);
        @World.addEntity(shot);

        angle += 10f;
    }
}

task fireAfterDelay(shot: Shot, delay: int, speed: float) {
    wait(delay);
    shot.setSpeed(speed, 0f);
}